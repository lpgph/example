plugins {
    id "java"
    id "idea"
    id "eclipse"
    id 'maven-publish'
    id 'org.springframework.boot' version '2.3.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'com.bmuschko.docker-spring-boot-application' version '6.6.1'
}
description = "资源服务-商品 web和webflux混合   主要使用web"

group = "io.lpgph"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


repositories {
    mavenLocal()
}

sourceSets {
    main {
        java {
            srcDir("src/main/java") // 指定源码目录
        }
        resources {
            srcDirs = ["src/main/resources", "src/main/java"] //指定资源目录
            includes = ["**/*"]
            excludes = ["**/*.java,**/*.kt"]
        }
    }
    test {
        java {
            srcDir("src/test/java") // 指定源码目录
        }
        resources {
            srcDirs = ["src/test/resources", "src/test/java"] //指定资源目录
            includes = ["**/*"]
            excludes = ["**/*.java,**/*.kt"]
        }
    }
}

//定义jar包版本号
ext {
    set("springCloudVersion", "Hoxton.SR9")
    set("jacksonVersion", "2.10.3")
    set("poi_version", "4.1.0")
    set('springCloudAlibabaVersion', "2.2.3.RELEASE")

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"
    javadoc.options.encoding = "UTF-8"
}


configurations {
    // spring热部署
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    //##############################   spring cloud   ##############################
    implementation("org.springframework.cloud:spring-cloud-starter-openfeign")

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation('org.springframework.boot:spring-boot-starter-oauth2-client')

    implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config'
    implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery'
    // 支持jdk8+
    implementation("com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}")

    //##############################   spring boot   ##############################
    //监控
    implementation("org.springframework.boot:spring-boot-starter-actuator")

    implementation 'com.google.guava:guava:29.0-jre'

    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
    }
}
configurations.all {
    resolutionStrategy.dependencySubstitution.all { dependency ->
        if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.module == 'spring-boot-starter-logging') {
            dependency.useTarget("org.springframework.boot:spring-boot-starter-log4j2:$dependency.requested.version", 'Use Log4j2 instead of Logback')
        }
    }
}

test {
    useJUnitPlatform()
}

//docker {
//    springBootApplication {
//        baseImage = 'openjdk:8-alpine'
//        ports = [9090, 8080]
//        images = ['goods:' + project.version, 'goods:latest']
//        jvmArgs = ['-Dspring.profiles.active=production', '-Xmx2048m']
//    }
//}
